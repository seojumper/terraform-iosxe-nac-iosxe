resource "iosxe_bgp" "bgp" {
  for_each = { for device in local.devices : device.name => device if try(local.device_config[device.name].routing.bgp, null) != null }
  device   = each.value.name

  asn                  = try(local.device_config[each.value.name].routing.bgp.as_number, local.defaults.iosxe.configuration.routing.bgp.as_number, null)
  default_ipv4_unicast = try(local.device_config[each.value.name].routing.bgp.default_ipv4_unicast, local.defaults.iosxe.configuration.routing.bgp.default_ipv4_unicast, null)
  log_neighbor_changes = try(local.device_config[each.value.name].routing.bgp.log_neighbor_changes, local.defaults.iosxe.configuration.routing.bgp.log_neighbor_changes, null)
  router_id_loopback   = try(local.device_config[each.value.name].routing.bgp.router_id_interface_type, local.defaults.iosxe.configuration.routing.bgp.router_id_interface_type, null) == "Loopback" ? try(local.device_config[each.value.name].routing.bgp.router_id_interface_id, local.defaults.iosxe.configuration.routing.bgp.router_id_interface_id, null) : null
  router_id_ip         = try(local.device_config[each.value.name].routing.bgp.router_id_ip, local.defaults.iosxe.configuration.routing.bgp.router_id_ip, null)
  bgp_graceful_restart = try(local.device_config[each.value.name].routing.bgp.bgp_graceful_restart, local.defaults.iosxe.configuration.routing.bgp.bgp_graceful_restart, null)
  bgp_update_delay     = try(local.device_config[each.value.name].routing.bgp.bgp_update_delay, local.defaults.iosxe.configuration.routing.bgp.bgp_update_delay, null)

  depends_on = [
    iosxe_interface_loopback.loopback,
    iosxe_system.system
  ]
}

locals {
  bgp_peer_session_templates = flatten([
    for device in local.devices : [
      for template in try(local.device_config[device.name].routing.bgp.peer_session_templates, []) : {
        key                              = format("%s/%s", device.name, template.template_name)
        device                           = device.name
        asn                              = iosxe_bgp.bgp[device.name].asn
        template_name                    = try(template.template_name, null)
        remote_as                        = try(template.remote_as, local.defaults.iosxe.configuration.routing.bgp.peer_session_templates.remote_as, null)
        description                      = try(template.description, local.defaults.iosxe.configuration.routing.bgp.peer_session_templates.description, null)
        disable_connected_check          = try(template.disable_connected_check, local.defaults.iosxe.configuration.routing.bgp.peer_session_templates.disable_connected_check, null)
        ebgp_multihop                    = try(template.ebgp_multihop, local.defaults.iosxe.configuration.routing.bgp.peer_session_templates.ebgp_multihop, null)
        ebgp_multihop_max_hop            = try(template.ebgp_multihop_max_hop, local.defaults.iosxe.configuration.routing.bgp.peer_session_templates.ebgp_multihop_max_hop, null)
        update_source_interface_loopback = try(template.update_source_interface_type, local.defaults.iosxe.configuration.routing.bgp.peer_session_templates.update_source_interface_type, null) == "Loopback" ? try(template.update_source_interface_id, local.defaults.iosxe.configuration.routing.bgp.peer_session_templates.update_source_interface_id, null) : null
        inherit_peer_session             = try(template.inherit_peer_session, local.defaults.iosxe.configuration.routing.bgp.peer_session_templates.inherit_peer_session, null)
      }
    ]
  ])
}

resource "iosxe_bgp_peer_session_template" "bgp_peer_session_template" {
  for_each = { for e in local.bgp_peer_session_templates : e.key => e }
  device   = each.value.device

  asn                              = each.value.asn
  template_name                    = each.value.template_name
  remote_as                        = each.value.remote_as
  description                      = each.value.description
  disable_connected_check          = each.value.disable_connected_check
  ebgp_multihop                    = each.value.ebgp_multihop
  ebgp_multihop_max_hop            = each.value.ebgp_multihop_max_hop
  update_source_interface_loopback = each.value.update_source_interface_loopback
  inherit_peer_session             = each.value.inherit_peer_session

  depends_on = [
    iosxe_bgp.bgp,
    iosxe_interface_loopback.loopback
  ]
}

locals {
  bgp_neighbors = flatten([
    for device in local.devices : [
      for neighbor in try(local.device_config[device.name].routing.bgp.neighbors, []) : {
        key                                       = format("%s/%s", device.name, neighbor.ip)
        device                                    = device.name
        asn                                       = iosxe_bgp.bgp[device.name].asn
        ip                                        = try(neighbor.ip, null)
        remote_as                                 = try(neighbor.remote_as, local.defaults.iosxe.configuration.routing.bgp.neighbors.remote_as, null)
        description                               = try(neighbor.description, local.defaults.iosxe.configuration.routing.bgp.neighbors.description, null)
        shutdown                                  = try(neighbor.shutdown, local.defaults.iosxe.configuration.routing.bgp.neighbors.shutdown, null)
        cluster_id                                = try(neighbor.cluster_id, local.defaults.iosxe.configuration.routing.bgp.neighbors.cluster_id, null)
        disable_connected_check                   = try(neighbor.disable_connected_check, local.defaults.iosxe.configuration.routing.bgp.neighbors.disable_connected_check, null)
        fall_over_default_enable                  = try(neighbor.fall_over_default_enable, local.defaults.iosxe.configuration.routing.bgp.neighbors.fall_over_default_enable, null)
        fall_over_default_route_map               = try(neighbor.fall_over_default_route_map, local.defaults.iosxe.configuration.routing.bgp.neighbors.fall_over_default_route_map, null)
        fall_over_bfd                             = try(neighbor.fall_over_bfd, local.defaults.iosxe.configuration.routing.bgp.neighbors.fall_over_bfd, null)
        fall_over_bfd_multi_hop                   = try(neighbor.fall_over_bfd_multi_hop, local.defaults.iosxe.configuration.routing.bgp.neighbors.fall_over_bfd_multi_hop, null)
        fall_over_bfd_single_hop                  = try(neighbor.fall_over_bfd_single_hop, local.defaults.iosxe.configuration.routing.bgp.neighbors.fall_over_bfd_single_hop, null)
        fall_over_bfd_check_control_plane_failure = try(neighbor.fall_over_bfd_check_control_plane_failure, local.defaults.iosxe.configuration.routing.bgp.neighbors.fall_over_bfd_check_control_plane_failure, null)
        fall_over_bfd_strict_mode                 = try(neighbor.fall_over_bfd_strict_mode, local.defaults.iosxe.configuration.routing.bgp.neighbors.fall_over_bfd_strict_mode, null)
        fall_over_maximum_metric_route_map        = try(neighbor.fall_over_maximum_metric_route_map, local.defaults.iosxe.configuration.routing.bgp.neighbors.fall_over_maximum_metric_route_map, null)
        local_as                                  = try(neighbor.local_as, local.defaults.iosxe.configuration.routing.bgp.neighbors.local_as, null)
        local_as_no_prepend                       = try(neighbor.local_as_no_prepend, local.defaults.iosxe.configuration.routing.bgp.neighbors.local_as_no_prepend, null)
        local_as_replace_as                       = try(neighbor.local_as_replace_as, local.defaults.iosxe.configuration.routing.bgp.neighbors.local_as_replace_as, null)
        local_as_dual_as                          = try(neighbor.local_as_dual_as, local.defaults.iosxe.configuration.routing.bgp.neighbors.local_as_dual_as, null)
        log_neighbor_changes                      = try(neighbor.log_neighbor_changes, local.defaults.iosxe.configuration.routing.bgp.neighbors.log_neighbor_changes, null)
        password_type                             = try(neighbor.password_type, local.defaults.iosxe.configuration.routing.bgp.neighbors.password_type, null)
        password                                  = try(neighbor.password, local.defaults.iosxe.configuration.routing.bgp.neighbors.password, null)
        peer_group                                = try(neighbor.peer_group, local.defaults.iosxe.configuration.routing.bgp.neighbors.peer_group, null)
        timers_keepalive_interval                 = try(neighbor.timers_keepalive, local.defaults.iosxe.configuration.routing.bgp.neighbors.timers_keepalive, null)
        timers_holdtime                           = try(neighbor.timers_holdtime, local.defaults.iosxe.configuration.routing.bgp.neighbors.timers_holdtime, null)
        timers_minimum_neighbor_hold              = try(neighbor.timers_minimum_neighbor_holdtime, local.defaults.iosxe.configuration.routing.bgp.neighbors.timers_minimum_neighbor_holdtime, null)
        ttl_security_hops                         = try(neighbor.ttl_security_hops, local.defaults.iosxe.configuration.routing.bgp.neighbors.ttl_security_hops, null)
        update_source_interface_loopback          = try(neighbor.update_source_interface_type, local.defaults.iosxe.configuration.routing.bgp.neighbors.update_source_interface_type, null) == "Loopback" ? try(neighbor.update_source_interface_id, local.defaults.iosxe.configuration.routing.bgp.neighbors.update_source_interface_id, null) : null
        ebgp_multihop                             = try(neighbor.ebgp_multihop, local.defaults.iosxe.configuration.routing.bgp.neighbors.ebgp_multihop, null)
        ebgp_multihop_max_hop                     = try(neighbor.ebgp_multihop_max_hop, local.defaults.iosxe.configuration.routing.bgp.neighbors.ebgp_multihop_max_hop, null)
        inherit_peer_session                      = try(neighbor.inherit_peer_session, local.defaults.iosxe.configuration.routing.bgp.neighbors.inherit_peer_session, null)
      }
    ]
  ])
}

resource "iosxe_bgp_neighbor" "bgp_neighbor" {
  for_each = { for e in local.bgp_neighbors : e.key => e }
  device   = each.value.device

  asn                                       = each.value.asn
  ip                                        = each.value.ip
  remote_as                                 = each.value.remote_as
  description                               = each.value.description
  shutdown                                  = each.value.shutdown
  cluster_id                                = each.value.cluster_id
  disable_connected_check                   = each.value.disable_connected_check
  fall_over_default_enable                  = each.value.fall_over_default_enable
  fall_over_default_route_map               = each.value.fall_over_default_route_map
  fall_over_bfd                             = each.value.fall_over_bfd
  fall_over_bfd_multi_hop                   = each.value.fall_over_bfd_multi_hop
  fall_over_bfd_single_hop                  = each.value.fall_over_bfd_single_hop
  fall_over_bfd_check_control_plane_failure = each.value.fall_over_bfd_check_control_plane_failure
  fall_over_bfd_strict_mode                 = each.value.fall_over_bfd_strict_mode
  fall_over_maximum_metric_route_map        = each.value.fall_over_maximum_metric_route_map
  local_as                                  = each.value.local_as
  local_as_no_prepend                       = each.value.local_as_no_prepend
  local_as_replace_as                       = each.value.local_as_replace_as
  local_as_dual_as                          = each.value.local_as_dual_as
  log_neighbor_changes                      = each.value.log_neighbor_changes
  password_type                             = each.value.password_type
  password                                  = each.value.password
  peer_group                                = each.value.peer_group
  timers_keepalive_interval                 = each.value.timers_keepalive_interval
  timers_holdtime                           = each.value.timers_holdtime
  timers_minimum_neighbor_hold              = each.value.timers_minimum_neighbor_hold
  ttl_security_hops                         = each.value.ttl_security_hops
  update_source_interface_loopback          = each.value.update_source_interface_loopback
  ebgp_multihop                             = each.value.ebgp_multihop
  ebgp_multihop_max_hop                     = each.value.ebgp_multihop_max_hop
  inherit_peer_session                      = each.value.inherit_peer_session

  depends_on = [
    iosxe_bgp.bgp,
    iosxe_bgp_peer_session_template.bgp_peer_session_template,
    iosxe_bgp_address_family_ipv4.bgp_address_family_ipv4
  ]
}

locals {
  template_peer_policies = flatten([
    for device in local.devices : [
      for peer_policy in try(local.device_config[device.name].routing.bgp.template_peer_policies, []) : {
        key                       = format("%s/%s", device.name, peer_policy.name)
        device                    = device.name
        asn                       = iosxe_bgp.bgp[device.name].asn
        name                      = try(peer_policy.name, null)
        send_community            = try(peer_policy.send_community, null)
        route_reflector_client    = try(peer_policy.route_reflector_client, null)
        allowas_in_as_number      = try(peer_policy.allowas_in_as_number, null)
        as_override_split_horizon = try(peer_policy.as_override_split_horizon, null)
        route_maps = try(length(peer_policy.route_maps) == 0, true) ? null : [for route_map in peer_policy.route_maps : {
          in_out         = try(route_map.in_out, null)
          route_map_name = try(route_map.route_map_name, null)
        }]
      }
    ]
  ])
}

resource "iosxe_bgp_peer_policy_template" "bgp_peer_policy_template" {
  for_each = { for e in local.template_peer_policies : e.key => e }
  device   = each.value.device

  asn                       = each.value.asn
  name                      = each.value.name
  send_community            = each.value.send_community
  route_reflector_client    = each.value.route_reflector_client
  allowas_in_as_number      = each.value.allowas_in_as_number
  as_override_split_horizon = each.value.as_override_split_horizon
  route_maps                = each.value.route_maps

  depends_on = [iosxe_route_map.route_map]
}

resource "iosxe_bgp_address_family_ipv4" "bgp_address_family_ipv4" {
  for_each = { for device in local.devices : device.name => device if try(local.device_config[device.name].routing.bgp.address_family.ipv4_unicast, null) != null }
  device   = each.value.name

  asn                                 = iosxe_bgp.bgp[each.value.name].asn
  af_name                             = "unicast"
  ipv4_unicast_redistribute_connected = try(local.device_config[each.value.name].routing.bgp.address_family.ipv4_unicast.redistribute.connected, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.redistribute.connected, null)
  ipv4_unicast_redistribute_static    = try(local.device_config[each.value.name].routing.bgp.address_family.ipv4_unicast.redistribute.static, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.redistribute.static, null)
  ipv4_unicast_distance_bgp_external  = try(local.device_config[each.value.name].routing.bgp.address_family.ipv4_unicast.distance_bgp_external, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.distance_bgp_external, null)
  ipv4_unicast_distance_bgp_internal  = try(local.device_config[each.value.name].routing.bgp.address_family.ipv4_unicast.distance_bgp_internal, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.distance_bgp_internal, null)
  ipv4_unicast_distance_bgp_local     = try(local.device_config[each.value.name].routing.bgp.address_family.ipv4_unicast.distance_bgp_local, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.distance_bgp_local, null)
  ipv4_unicast_aggregate_addresses = try(length(local.device_config[each.value.name].routing.bgp.address_family.ipv4_unicast.aggregate_addresses) == 0, true) ? null : [for agg in local.device_config[each.value.name].routing.bgp.address_family.ipv4_unicast.aggregate_addresses : {
    ipv4_address = try(agg.address, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.aggregate_addresses.address, null)
    ipv4_mask    = try(agg.mask, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.aggregate_addresses.mask, null)
  }]
  ipv4_unicast_networks_mask = try(length(local.device_config[each.value.name].routing.bgp.address_family.ipv4_unicast.networks) == 0, true) ? null : [for net in local.device_config[each.value.name].routing.bgp.address_family.ipv4_unicast.networks : {
    network   = try(net.network, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.networks.network, null)
    mask      = try(net.mask, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.networks.mask, null)
    route_map = try(net.route_map, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.networks.route_map, null)
    backdoor  = try(net.backdoor, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.networks.backdoor, null)
  } if try(net.mask, null) != null]
  ipv4_unicast_networks = try(length(local.device_config[each.value.name].routing.bgp.address_family.ipv4_unicast.networks) == 0, true) ? null : [for net in local.device_config[each.value.name].routing.bgp.address_family.ipv4_unicast.networks : {
    network   = try(net.network, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.networks.network, null)
    route_map = try(net.route_map, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.networks.route_map, null)
    backdoor  = try(net.backdoor, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.networks.backdoor, null)
  } if try(net.mask, null) == null]
  ipv4_unicast_admin_distances = try(length(local.device_config[each.value.name].routing.bgp.address_family.ipv4_unicast.admin_distances) == 0, true) ? null : [for ad in local.device_config[each.value.name].routing.bgp.address_family.ipv4_unicast.admin_distances : {
    distance  = try(ad.distance, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.admin_distances.distance, null)
    source_ip = try(ad.source_ip, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.admin_distances.source_ip, null)
    wildcard  = try(ad.wildcard, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.admin_distances.wildcard, null)
    acl       = try(ad.acl, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.admin_distances.acl, null)
  }]
  ipv4_unicast_maximum_paths_ebgp = try(local.device_config[each.value.name].routing.bgp.address_family.ipv4_unicast.ipv4_unicast_maximum_paths_ebgp, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.ipv4_unicast_maximum_paths_ebgp, null)
  ipv4_unicast_maximum_paths_ibgp = try(local.device_config[each.value.name].routing.bgp.address_family.ipv4_unicast.ipv4_unicast_maximum_paths_ibgp, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.ipv4_unicast_maximum_paths_ibgp, null)

  depends_on = [
    iosxe_access_list_standard.access_list_standard,
    iosxe_access_list_extended.access_list_extended
  ]
}

resource "iosxe_bgp_address_family_ipv6" "bgp_address_family_ipv6" {
  for_each = { for device in local.devices : device.name => device if try(local.device_config[device.name].routing.bgp.address_family.ipv6_unicast, null) != null }
  device   = each.value.name

  asn                                 = iosxe_bgp.bgp[each.value.name].asn
  af_name                             = "unicast"
  ipv6_unicast_redistribute_connected = try(local.device_config[each.value.name].routing.bgp.address_family.ipv6_unicast.redistribute.connected, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv6_unicast.redistribute.connected, null)
  ipv6_unicast_redistribute_static    = try(local.device_config[each.value.name].routing.bgp.address_family.ipv6_unicast.redistribute.static, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv6_unicast.redistribute.static, null)
  ipv6_unicast_networks = try(length(local.device_config[each.value.name].routing.bgp.address_family.ipv6_unicast.networks) == 0, true) ? null : [for net in local.device_config[each.value.name].routing.bgp.address_family.ipv6_unicast.networks : {
    network   = try(net.network, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv6_unicast.networks.network, null)
    route_map = try(net.route_map, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv6_unicast.networks.route_map, null)
    backdoor  = try(net.backdoor, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv6_unicast.networks.backdoor, null)
  }]


}

resource "iosxe_bgp_address_family_l2vpn" "bgp_address_family_l2vpn" {
  for_each = { for device in local.devices : device.name => device if try(local.device_config[device.name].routing.bgp.address_family.l2vpn_evpn, null) != null }
  device   = each.value.name

  asn                       = iosxe_bgp.bgp[each.value.name].asn
  af_name                   = "evpn"
  rewrite_evpn_rt_asn       = try(local.device_config[each.value.name].routing.bgp.address_family.l2vpn_evpn.rewrite_evpn_rt_asn, local.defaults.iosxe.configuration.routing.bgp.address_family.l2vpn_evpn.rewrite_evpn_rt_asn, null)
  bgp_nexthop_trigger_delay = try(local.device_config[each.value.name].routing.bgp.address_family.l2vpn_evpn.bgp_nexthop_trigger_delay, local.defaults.iosxe.configuration.routing.bgp.address_family.l2vpn_evpn.bgp_nexthop_trigger_delay, null)
}

resource "iosxe_bgp_address_family_ipv4_vrf" "bgp_address_family_ipv4_vrf" {
  for_each = { for device in local.devices : device.name => device if try(local.device_config[device.name].routing.bgp.address_family.ipv4_unicast.vrfs, null) != null }
  device   = each.value.name

  asn     = iosxe_bgp.bgp[each.value.name].asn
  af_name = "unicast"
  vrfs = try(length(local.device_config[each.value.name].routing.bgp.address_family.ipv4_unicast.vrfs) == 0, true) ? null : [for vrf in local.device_config[each.value.name].routing.bgp.address_family.ipv4_unicast.vrfs : {
    name                                = vrf.vrf
    ipv4_unicast_advertise_l2vpn_evpn   = try(vrf.advertise_l2vpn_evpn, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.advertise_l2vpn_evpn, null)
    ipv4_unicast_redistribute_connected = try(vrf.redistribute.connected, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.redistribute.connected, null)
    ipv4_unicast_router_id_ip           = try(vrf.router_id, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.router_id, null)
    ipv4_unicast_router_id_loopback     = try(vrf.router_id_interface_type, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.router_id_interface_type, null) == "Loopback" ? try(vrf.router_id_interface_id, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.router_id_interface_id, null) : null
    ipv4_unicast_distance_bgp_external  = try(vrf.distance_bgp_external, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.distance_bgp_external, null)
    ipv4_unicast_distance_bgp_internal  = try(vrf.distance_bgp_internal, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.distance_bgp_internal, null)
    ipv4_unicast_distance_bgp_local     = try(vrf.distance_bgp_local, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.distance_bgp_local, null)
    ipv4_unicast_aggregate_addresses = try(length(vrf.aggregate_addresses) == 0, true) ? null : [for agg in vrf.aggregate_addresses : {
      ipv4_address = try(agg.address, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.aggregate_addresses.address, null)
      ipv4_mask    = try(agg.mask, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.aggregate_addresses.mask, null)
      summary_only = try(agg.summary_only, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.aggregate_addresses.summary_only, null)
    }]
    ipv4_unicast_admin_distances = try(length(vrf.admin_distances) == 0, true) ? null : [for ad in vrf.admin_distances : {
      distance  = try(ad.distance, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.admin_distances.distance, null)
      source_ip = try(ad.source_ip, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.admin_distances.source_ip, null)
      wildcard  = try(ad.wildcard, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.admin_distances.wildcard, null)
      acl       = try(ad.acl, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.admin_distances.acl, null)
    }]
    ipv4_unicast_redistribute_static = try(vrf.redistribute.static, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.redistribute.static, null)
    ipv4_unicast_networks_mask = try(length(vrf.networks) == 0, true) ? null : [for net in vrf.networks : {
      network   = try(net.network, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.networks.network, null)
      mask      = try(net.mask, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.networks.mask, null)
      route_map = try(net.route_map, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.networks.route_map, null)
      backdoor  = try(net.backdoor, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.networks.backdoor, null)
    } if try(net.mask, null) != null]
    ipv4_unicast_networks = try(length(vrf.networks) == 0, true) ? null : [for net in vrf.networks : {
      network   = try(net.network, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.networks.network, null)
      route_map = try(net.route_map, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.networks.route_map, null)
      backdoor  = try(net.backdoor, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.networks.backdoor, null)
    } if try(net.mask, null) == null]
    ipv4_unicast_maximum_paths_ebgp        = try(vrf.ipv4_unicast_maximum_paths_ebgp, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.ipv4_unicast_maximum_paths_ebgp, null)
    ipv4_unicast_maximum_paths_ibgp        = try(vrf.ipv4_unicast_maximum_paths_ibgp, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.ipv4_unicast_maximum_paths_ibgp, null)
    ipv4_unicast_import_path_selection_all = try(vrf.import_path_selection_all, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.import_path_selection_all, null)
  }]

  depends_on = [
    iosxe_vrf.vrf,
    iosxe_access_list_standard.access_list_standard,
    iosxe_access_list_extended.access_list_extended
  ]
}

resource "iosxe_bgp_address_family_ipv6_vrf" "bgp_address_family_ipv6_vrf" {
  for_each = { for device in local.devices : device.name => device if try(local.device_config[device.name].routing.bgp.address_family.ipv6_unicast.vrfs, null) != null }
  device   = each.value.name

  asn     = iosxe_bgp.bgp[each.value.name].asn
  af_name = "unicast"
  vrfs = try(length(local.device_config[each.value.name].routing.bgp.address_family.ipv6_unicast.vrfs) == 0, true) ? null : [for vrf in local.device_config[each.value.name].routing.bgp.address_family.ipv6_unicast.vrfs : {
    name                                = vrf.vrf
    ipv6_unicast_advertise_l2vpn_evpn   = try(vrf.advertise_l2vpn_evpn, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv6_unicast.vrfs.advertise_l2vpn_evpn, null)
    ipv6_unicast_redistribute_connected = try(vrf.redistribute.connected, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv6_unicast.vrfs.redistribute.connected, null)
    ipv6_unicast_redistribute_static    = try(vrf.redistribute.static, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv6_unicast.vrfs.redistribute.static, null)
    ipv6_unicast_networks = try(length(vrf.networks) == 0, true) ? null : [for net in vrf.networks : {
      network   = try(net.network, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv6_unicast.vrfs.networks.network, null)
      route_map = try(net.route_map, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv6_unicast.vrfs.networks.route_map, null)
      backdoor  = try(net.backdoor, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv6_unicast.vrfs.networks.backdoor, null)
      evpn      = try(net.evpn, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv6_unicast.vrfs.networks.evpn, null)
    }]
  }]

  depends_on = [
    iosxe_vrf.vrf
  ]
}

locals {
  bgp_ipv4_unicast_neighbors = flatten([
    for device in local.devices : [
      for neighbor in try(local.device_config[device.name].routing.bgp.address_family.ipv4_unicast.neighbors, []) : {
        key                         = format("%s/%s", device.name, neighbor.ip)
        device                      = device.name
        asn                         = iosxe_bgp.bgp[device.name].asn
        ip                          = neighbor.ip
        activate                    = try(neighbor.activate, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.neighbors.activate, true)
        send_community              = try(neighbor.send_community, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.neighbors.send_community, null)
        route_reflector_client      = try(neighbor.route_reflector_client, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.neighbors.route_reflector_client, null)
        soft_reconfiguration        = try(neighbor.soft_reconfiguration, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.neighbors.soft_reconfiguration, null)
        default_originate           = try(neighbor.default_originate, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.neighbors.default_originate, null)
        default_originate_route_map = try(neighbor.default_originate_route_map, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.neighbors.default_originate_route_map, null)
        route_maps = try(length(neighbor.route_maps) == 0, true) ? null : [for rm in neighbor.route_maps : {
          in_out         = try(rm.direction, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.neighbors.route_maps.direction, null)
          route_map_name = try(rm.name, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.neighbors.route_maps.name, null)
        }]
      }
    ]
  ])
}

resource "iosxe_bgp_ipv4_unicast_neighbor" "bgp_ipv4_unicast_neighbor" {
  for_each = { for e in local.bgp_ipv4_unicast_neighbors : e.key => e }
  device   = each.value.device

  asn                         = each.value.asn
  ip                          = each.value.ip
  activate                    = each.value.activate
  send_community              = each.value.send_community
  route_reflector_client      = each.value.route_reflector_client
  soft_reconfiguration        = each.value.soft_reconfiguration
  default_originate           = each.value.default_originate
  default_originate_route_map = each.value.default_originate_route_map
  route_maps                  = each.value.route_maps

  depends_on = [
    iosxe_bgp_neighbor.bgp_neighbor,
    iosxe_bgp_address_family_ipv4.bgp_address_family_ipv4
  ]
}

locals {
  bgp_ipv6_unicast_neighbors = flatten([
    for device in local.devices : [
      for neighbor in try(local.device_config[device.name].routing.bgp.address_family.ipv6_unicast.neighbors, []) : {
        key                         = format("%s/%s", device.name, neighbor.ip)
        device                      = device.name
        asn                         = iosxe_bgp.bgp[device.name].asn
        ip                          = neighbor.ip
        activate                    = try(neighbor.activate, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv6_unicast.neighbors.activate, true)
        send_community              = try(neighbor.send_community, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv6_unicast.neighbors.send_community, null)
        route_reflector_client      = try(neighbor.route_reflector_client, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv6_unicast.neighbors.route_reflector_client, null)
        soft_reconfiguration        = try(neighbor.soft_reconfiguration, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv6_unicast.neighbors.soft_reconfiguration, null)
        default_originate           = try(neighbor.default_originate, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv6_unicast.neighbors.default_originate, null)
        default_originate_route_map = try(neighbor.default_originate_route_map, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv6_unicast.neighbors.default_originate_route_map, null)
        route_maps = try(length(neighbor.route_maps) == 0, true) ? null : [for rm in neighbor.route_maps : {
          in_out         = try(rm.in_out, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv6_unicast.neighbors.route_maps.in_out, null)
          route_map_name = try(rm.route_map_name, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv6_unicast.neighbors.route_maps.route_map_name, null)
        }]
      }
    ]
  ])
}

resource "iosxe_bgp_ipv6_unicast_neighbor" "bgp_ipv6_unicast_neighbor" {
  for_each = { for e in local.bgp_ipv6_unicast_neighbors : e.key => e }
  device   = each.value.device

  asn                         = each.value.asn
  ip                          = each.value.ip
  activate                    = each.value.activate
  send_community              = each.value.send_community
  route_reflector_client      = each.value.route_reflector_client
  soft_reconfiguration        = each.value.soft_reconfiguration
  default_originate           = each.value.default_originate
  default_originate_route_map = each.value.default_originate_route_map
  route_maps                  = each.value.route_maps

  depends_on = [
    iosxe_bgp_neighbor.bgp_neighbor,
    iosxe_bgp_address_family_ipv6.bgp_address_family_ipv6
  ]
}

locals {
  bgp_l2vpn_evpn_neighbors = flatten([
    for device in local.devices : [
      for neighbor in try(local.device_config[device.name].routing.bgp.address_family.l2vpn_evpn.neighbors, []) : {
        key                    = format("%s/%s", device.name, neighbor.ip)
        device                 = device.name
        asn                    = iosxe_bgp.bgp[device.name].asn
        ip                     = neighbor.ip
        activate               = try(neighbor.activate, local.defaults.iosxe.configuration.routing.bgp.address_family.l2vpn_evpn.neighbors.activate, true)
        send_community         = try(neighbor.send_community, local.defaults.iosxe.configuration.routing.bgp.address_family.l2vpn_evpn.neighbors.send_community, null)
        route_reflector_client = try(neighbor.route_reflector_client, local.defaults.iosxe.configuration.routing.bgp.address_family.l2vpn_evpn.neighbors.route_reflector_client, null)
        soft_reconfiguration   = try(neighbor.soft_reconfiguration, local.defaults.iosxe.configuration.routing.bgp.address_family.l2vpn_evpn.neighbors.soft_reconfiguration, null)
        route_maps = try(length(neighbor.route_maps) == 0, true) ? null : [for rm in neighbor.route_maps : {
          in_out         = try(rm.in_out, local.defaults.iosxe.configuration.routing.bgp.address_family.l2vpn_evpn.neighbors.route_maps.in_out, null)
          route_map_name = try(rm.name, local.defaults.iosxe.configuration.routing.bgp.address_family.l2vpn_evpn.neighbors.route_maps.name, null)
        }]
      }
    ]
  ])
}

resource "iosxe_bgp_l2vpn_evpn_neighbor" "bgp_l2vpn_evpn_neighbor" {
  for_each = { for e in local.bgp_l2vpn_evpn_neighbors : e.key => e }
  device   = each.value.device

  asn                    = each.value.asn
  ip                     = each.value.ip
  activate               = each.value.activate
  send_community         = each.value.send_community
  route_reflector_client = each.value.route_reflector_client
  soft_reconfiguration   = each.value.soft_reconfiguration
  route_maps             = each.value.route_maps

  depends_on = [
    iosxe_bgp_neighbor.bgp_neighbor,
    iosxe_bgp_address_family_l2vpn.bgp_address_family_l2vpn,
    iosxe_route_map.route_map
  ]
}

locals {
  bgp_ipv4_unicast_vrf_neighbors = flatten([
    for device in local.devices : [
      for vrf in try(local.device_config[device.name].routing.bgp.address_family.ipv4_unicast.vrfs, []) : [
        for neighbor in try(vrf.neighbors, []) : {
          key                                       = format("%s/%s/%s", device.name, vrf.vrf, neighbor.ip)
          device                                    = device.name
          asn                                       = iosxe_bgp.bgp[device.name].asn
          vrf                                       = vrf.vrf
          ip                                        = neighbor.ip
          remote_as                                 = try(neighbor.remote_as, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.remote_as, null)
          description                               = try(neighbor.description, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.description, null)
          shutdown                                  = try(neighbor.shutdown, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.shutdown, null)
          cluster_id                                = try(neighbor.cluster_id, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.cluster_id, null)
          log_neighbor_changes_disable              = try(!neighbor.log_neighbor_changes, !local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.log_neighbor_changes, null)
          password_type                             = try(neighbor.password_type, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.password_type, null)
          password                                  = try(neighbor.password, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.password, null)
          timers_keepalive_interval                 = try(neighbor.timers_keepalive, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.timers_keepalive, null)
          timers_holdtime                           = try(neighbor.timers_holdtime, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.timers_holdtime, null)
          timers_minimum_neighbor_hold              = try(neighbor.timers_minimum_neighbor_holdtime, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.timers_minimum_neighbor_holdtime, null)
          fall_over_default_route_map               = try(neighbor.fall_over_default_route_map, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.fall_over_default_route_map, null)
          fall_over_bfd                             = try(neighbor.fall_over_bfd, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.fall_over_bfd, null)
          fall_over_bfd_multi_hop                   = try(neighbor.fall_over_bfd_multi_hop, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.fall_over_bfd_multi_hop, null)
          fall_over_bfd_single_hop                  = try(neighbor.fall_over_bfd_single_hop, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.fall_over_bfd_single_hop, null)
          fall_over_bfd_check_control_plane_failure = try(neighbor.fall_over_bfd_check_control_plane_failure, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.fall_over_bfd_check_control_plane_failure, null)
          fall_over_bfd_strict_mode                 = try(neighbor.fall_over_bfd_strict_mode, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.fall_over_bfd_strict_mode, null)
          fall_over_maximum_metric_route_map        = try(neighbor.fall_over_maximum_metric_route_map, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.fall_over_maximum_metric_route_map, null)
          disable_connected_check                   = try(neighbor.disable_connected_check, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.disable_connected_check, null)
          ttl_security_hops                         = try(neighbor.ttl_security_hops, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.ttl_security_hops, null)
          local_as                                  = try(neighbor.local_as, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.local_as, null)
          local_as_no_prepend                       = try(neighbor.local_as_no_prepend, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.local_as_no_prepend, null)
          local_as_replace_as                       = try(neighbor.local_as_replace_as, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.local_as_replace_as, null)
          local_as_dual_as                          = try(neighbor.local_as_dual_as, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.local_as_dual_as, null)
          update_source_interface_loopback          = try(neighbor.update_source_interface_type, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.update_source_interface_type, null) == "Loopback" ? try(neighbor.update_source_interface_id, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.update_source_interface_id, null) : null
          activate                                  = try(neighbor.activate, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.activate, true)
          send_community                            = try(neighbor.send_community, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.send_community, null)
          route_reflector_client                    = try(neighbor.route_reflector_client, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.route_reflector_client, null)
          soft_reconfiguration                      = try(neighbor.soft_reconfiguration, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.soft_reconfiguration, null)
          default_originate                         = try(neighbor.default_originate, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.default_originate, null)
          default_originate_route_map               = try(neighbor.default_originate_route_map, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.default_originate_route_map, null)
          route_maps = try(length(neighbor.route_maps) == 0, true) ? null : [for rm in neighbor.route_maps : {
            in_out         = try(rm.direction, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.route_maps.direction, null)
            route_map_name = try(rm.name, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.route_maps.name, null)
          }]
          ebgp_multihop            = try(neighbor.ebgp_multihop, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.ebgp_multihop, null)
          ebgp_multihop_max_hop    = try(neighbor.ebgp_multihop_max_hop, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.ebgp_multihop_max_hop, null)
          ha_mode_graceful_restart = try(neighbor.ha_mode_graceful_restart, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.ha_mode_graceful_restart, null)
          next_hop_self            = try(neighbor.next_hop_self, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.next_hop_self, null)
          next_hop_self_all        = try(neighbor.next_hop_self_all, local.defaults.iosxe.configuration.routing.bgp.address_family_ipv4_unicast.vrfs.neighbors.next_hop_self_all, null)
          advertisement_interval   = try(neighbor.advertisement_interval, local.defaults.iosxe.configuration.routing.bgp.address_family.ipv4_unicast.vrfs.neighbors.advertisement_interval, null)
        }
      ]
    ]
  ])
}

resource "iosxe_bgp_ipv4_unicast_vrf_neighbor" "bgp_ipv4_unicast_vrf_neighbor" {
  for_each = { for e in local.bgp_ipv4_unicast_vrf_neighbors : e.key => e }
  device   = each.value.device

  asn                                       = each.value.asn
  vrf                                       = each.value.vrf
  ip                                        = each.value.ip
  remote_as                                 = each.value.remote_as
  description                               = each.value.description
  shutdown                                  = each.value.shutdown
  cluster_id                                = each.value.cluster_id
  log_neighbor_changes_disable              = each.value.log_neighbor_changes_disable
  password_type                             = each.value.password_type
  password                                  = each.value.password
  timers_keepalive_interval                 = each.value.timers_keepalive_interval
  timers_holdtime                           = each.value.timers_holdtime
  timers_minimum_neighbor_hold              = each.value.timers_minimum_neighbor_hold
  fall_over_default_route_map               = each.value.fall_over_default_route_map
  fall_over_bfd                             = each.value.fall_over_bfd
  fall_over_bfd_multi_hop                   = each.value.fall_over_bfd_multi_hop
  fall_over_bfd_single_hop                  = each.value.fall_over_bfd_single_hop
  fall_over_bfd_check_control_plane_failure = each.value.fall_over_bfd_check_control_plane_failure
  fall_over_bfd_strict_mode                 = each.value.fall_over_bfd_strict_mode
  fall_over_maximum_metric_route_map        = each.value.fall_over_maximum_metric_route_map
  disable_connected_check                   = each.value.disable_connected_check
  ttl_security_hops                         = each.value.ttl_security_hops
  local_as                                  = each.value.local_as
  local_as_no_prepend                       = each.value.local_as_no_prepend
  local_as_replace_as                       = each.value.local_as_replace_as
  local_as_dual_as                          = each.value.local_as_dual_as
  update_source_interface_loopback          = each.value.update_source_interface_loopback
  activate                                  = each.value.activate
  send_community                            = each.value.send_community
  route_reflector_client                    = each.value.route_reflector_client
  soft_reconfiguration                      = each.value.soft_reconfiguration
  default_originate                         = each.value.default_originate
  default_originate_route_map               = each.value.default_originate_route_map
  route_maps                                = each.value.route_maps
  ebgp_multihop                             = each.value.ebgp_multihop
  ebgp_multihop_max_hop                     = each.value.ebgp_multihop_max_hop
  ha_mode_graceful_restart                  = each.value.ha_mode_graceful_restart
  next_hop_self                             = each.value.next_hop_self
  next_hop_self_all                         = each.value.next_hop_self_all
  advertisement_interval                    = each.value.advertisement_interval

  depends_on = [
    iosxe_vrf.vrf,
    iosxe_bgp_address_family_ipv4_vrf.bgp_address_family_ipv4_vrf
  ]
}
